plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs'
}

// check README.md for versioning guideline
def versionMajor = 2
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    // TODO: signingConfigs can be kept private,
    //  included with build tasks to a remote system like jenkins.
    signingConfigs {
        release {
            storeFile file('../keystores/release.jks')
            storePassword 'releaseNoteApp123'
            keyAlias 'release'
            keyPassword 'releaseNoteApp123'
            v1SigningEnabled true
            v2SigningEnabled true
        }
        debug {
            storeFile file('../keystores/debug.jks')
            storePassword 'debugNoteApp123'
            keyAlias 'debug'
            keyPassword 'debugNoteApp123'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    defaultConfig {
        applicationId "com.task.noteapp"
        minSdkVersion 23
        targetSdkVersion 31
        // max versionCode value is 2_100_000_000
        versionCode 100_000 * versionMajor + 1_000 * versionMinor + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "com.task.noteapp.test_utils.HiltTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    android {
        packagingOptions {
            pickFirst 'META-INF/AL2.0'
            pickFirst 'META-INF/LGPL2.1'
        }
    }
}

dependencies {

    // Android Support Dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Navigation Components
    implementation "androidx.fragment:fragment-ktx:1.5.0-alpha03"
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.1"

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:2.38.1"
    kapt "com.google.dagger:hilt-android-compiler:2.37"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // Room - Database Dependencies
    implementation "androidx.room:room-runtime:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"

    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:2.4.2"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // Splash screen
    implementation "androidx.core:core-splashscreen:1.0.0-beta01"

    // Test Dependencies
    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2"

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "com.google.truth:truth:1.1.3"
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.37'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.37'
    androidTestAnnotationProcessor "com.google.dagger:hilt-android-compiler:2.38.1"
}

task exportDebugApk(type: Copy) {
    from('build/outputs/apk/debug/')
    into('release/')
    include('app-debug.apk')
    String versionName = "noteapp_debug_v${versionMajor}.${versionMinor}.${versionPatch}.apk"
    rename('app-debug.apk', versionName)
}

task exportReleaseApk(type: Copy) {
    from('build/outputs/apk/release/')
    into('release/')
    include('app-release.apk')
    String versionName = "noteapp_v${versionMajor}.${versionMinor}.${versionPatch}.apk"
    rename('app-release.apk', versionName)
}

exportDebugApk.dependsOn(build, assemble)
exportReleaseApk.dependsOn(build, assemble)
